// Original script by Unknown
// Edit by Mabuhay
// Removed my timer cool downs and stuff.
// Adjust to your time you wish for it to start.

-	script	Event_Bombring	-1,{
OnInit:
	// item recompensa
	setarray .item,
		35035, 2,
		35035, 1;

	// @bombstart - gm manual start
	bindatcmd "bombstart", strnpcinfo(0)+"::OnStart",60,60;
	
	// @bomb - player join event
	bindatcmd "bomb", strnpcinfo(0)+"::OnJoinEvent";
	
	// Mapflags
	setarray .@mapflag,
		mf_nowarp,
		mf_nowarpto,
		mf_noskill,
		mf_noteleport,
		mf_nomemo,
		mf_nosave,
		mf_noicewall,
		mf_nobranch,
		mf_noreturn,
		mf_nocommand;
		
	for ( .@i = 0; .@i < getarraysize(.@mapflag); .@i++)
		setmapflag "quiz_02", .@mapflag[.@i];
	end;
	
OnJoinEvent:
	mes " [ Event Status ] ";
	switch ( .start ) {
		case 1: .@status$ = "active"; break;
		case 2: .@status$ = "on-going"; break;
		default: .@status$ = "not active"; break;
	}
	mes "O evento está atualmente ^ff0000"+ .@status$ +"^000000";
	next;
	switch(select(
		"Evento Bombring - "+ ((.start == 1) ? "^0000ffParticipar do Evento":((.start == 2) ? "^0000ffEvento em andamento":"^ff0000Não disponível"))+"^000000" )) {
	case 1:
		if ( !.start ) {
			mes "Evento não disponível.";
			close;
		} else if ( .start == 2 ) {
			mes "O evento está em andamento";
			close;
		}
		sc_end SC_ALL;
		sc_start SC_DECREASEAGI,600000,10;
		percentheal 100,100;
		warp "quiz_02",59,345;
	}
	end;

OnStart:
// Add your clock timers here.
OnClock0000: // 12 MN
OnClock0300: // 3 AM
OnClock0600: // 6 AM
OnClock1700: // 5 PM
	if ( .start ) end;
	.start = 1;
	.round = 0;
	killmonsterall "quiz_02";
	announce "Bombring: Nós vamos ter um evento Bombring.",0;
	sleep 10000;
	announce "Bombring: Para quem quiser participar, use @bomb",0;
	sleep 10000;
	announce "Bombring: Após 1 minuto, o evento será fechado.",0;
	sleep 10000;
	announce "Bombring: Então, por favor, se você quiser participar. usar @bomb",0;
	sleep 60000;
	announce "Bombring: A entrada para o evento está encerrada.",bc_all,0x0ceedb;
	if ( getmapusers("quiz_02") < 1 ) {
		announce "Bombring: Participantes insuficientes.",bc_all,0x0ceedb;
		.start = false;
		mapwarp "quiz_02", "prontera", 150, 180; // warp out.
		end;
	}
	.start = 2;
	sleep 3500;
	mapannounce "quiz_02","Estamos prestes a começar o evento.",bc_map,0x0ceedb;
	sleep 2500;
	mapannounce "quiz_02","As regras são simples.",bc_map,0x0ceedb;
	sleep 2000;
	mapannounce "quiz_02","O evento será executado por 30 segundos por 5 rodadas totais.",bc_map,0x0ceedb;
	sleep 2500;
	mapannounce "quiz_02","Tudo o que você precisa fazer é evitar os Bombrings e permanecer vivo por 30 segundos.",bc_map,0x0ceedb;
	sleep 2000;
	mapannounce "quiz_02","Isso é tudo. Vamos começar.",bc_map,0x0ceedb;
	sleep 3000;
	while ( .round < 5 ) {
		.@count = getmapusers("quiz_02");
		.round++;
		mapannounce "quiz_02","Round "+.round,bc_map,0x0ceedb;
		areamonster "quiz_02",48,334,71,357,"Corra por sua vida!",1904, 5 * .@count * .round;
		sleep 10000 - ( 1000*.round );
	}
	.@count = getmapusers("quiz_02");
	if ( !.@count )
		announce "Bombring: Ninguém sobreviveu aos bombardeiros.",bc_all,0x0ceedb;
	else if ( .@count ) {
		.@size = getmapunits(BL_PC,"quiz_02",.@aid);
		.@amt = getarraysize(.item);
		if ( .@size > 1 ) announce "Muitos jogadores venceram o Evento Bombring.",bc_all,0x0ceedb;
		for ( .@i = 0; .@i < getarraysize(.@aid); .@i++) {
			attachrid .@aid[.@i];
			if ( .@size == 1 ) announce strcharinfo(0)+" venceu o evento Bombring.",bc_all,0x0ceedb;
			for ( .@j = 0; .@j < .@amt; .@j += 2 )
				getitem .item[.@j], .item[.@j+1];
		}
		detachrid;
	}
	.start = false;
	sleep 5000;
	mapwarp "quiz_02","prontera",rand(148,163),rand(167,186);
	end;
	
OnPCDieEvent:
	if ( .start && strcharinfo(3) == "quiz_02" )
		warp "prontera",rand(148,163),rand(167,186);
	end;
}