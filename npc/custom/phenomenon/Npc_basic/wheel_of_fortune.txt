//===== Hercules Script ======================================
//= Roda da Sorte
//===== By ===================================================
//= Habilis
//= Edited by Myriad
//===== Version ==============================================
//= 1.02 - June 15, 2018
//  - Made the script work with zeny, to be more accesibly to everyone.
//= 1.01 - June 14, 2018
//    - Added @wheel_of_fortune GM command (level 98+).
//    - Merged S_Pay and S_Loop subs into main line of text.
//    - S_Gamble is now S_Spin and it will 'return' instead of
//    calling back to the initial sub.
//= 1.00 - June 13, 2018
//    - Initial script edit for Habilis.
//===== Description ==========================================
//= Roda da Sorte something.
//============================================================


prontera,196,175,4	script	Roda da Sorte#Main	2_SLOT_MACHINE,{
    
OnTalk:

if (!countitem(35039) && #freewheelfortunespin < 0) {
        mes("[^0055FFGiro da sorte^000000]", .EventName$);
        mes("Você está sem Moedas da Sorte");
        mes("e não tem mais");
        mes("giros livres. Volte");
        mes("próxima vez para mais!");
        close();
    }

    .@mes$ = (#freewheelfortunespin > 0) ? sprintf(", mas você meu amigo, tem %d giro extra%s!", #freewheelfortunespin, (#freewheelfortunespin == 1) ? "" : "s") : ".";
    addtimer(1, sprintf("%s::OnEnd", strnpcinfo(NPC_NAME)));
    mes("[^0055FFGiro da sorte^000000]", .EventName$);
    mes("Você quer girar a roda?");
    mes("Isso custa ^FF00001 Moeda da Sorte^000000 para jogar", .@mes$);
    next();

    while (true) {
        if (!countitem(35039) && #freewheelfortunespin < 0)
            callsub(S_End);
        switch (select(
            (#freewheelfortunespin > 0) ? sprintf("Sim! Use rotação grátis! (%d left)", #freewheelfortunespin) : "",
            (countitem(35039)) ? "Sim!. (custa 1 Moeda da Sorte)" : "",
            "Não (sair)"
        )) {
        // pay with free spin
        case 1:
            if (#freewheelfortunespin > 0) {
                if ((#freewheelfortunespin -= 1) < 0)
                    #freewheelfortunespin = 0;
                callsub(S_Spin);
            } else
                callsub(S_End);
            break;

        // Pay with zeny
        case 2:
            if (countitem(35039)) {
                delitem 35039,1;
                callsub(S_Spin);
            } else {
                cutin("aca_salim02", 2);
                mes("[^0055FFGiro da sorte^000000]", .EventName$);
                mes("Awww, você não tem o suficiente para jogar ...");
                mes(" ");
                mes("Alguma vez ouviste?");
                mes("'Dinheiro não é tudo o que importa.");
                mes("Byeeeeeeeeeeeeee ;)");
                callsub(S_End);
            }
            break;

        default:
            callsub(S_End);
        }
    }

// Wheel spin animation
S_Spin:
    .@Sector = rand(.Sector_Range[0], .Sector_Range[1]);
    .@Display = .@Sector * 2 - 1;
    .@Speed = .Spin_Speed;
    
    for (.@i = 0; .@i < .nbTurns; .@i++) {
        .@b = .Cutin_Range[0];
        while (.@b <= .Cutin_Range[1]) {
            cutin(sprintf("%s%d", .Cutin$, .@b), 4);
            sleep2(.@Speed);
            .@b++;
            .@Speed += 1; // not ++, porque você pode querer ajustar a parada +1 +2 +3
        }
    }
    
    .@b = .Cutin_Range[0];
    while (.@b < .@Display) {
        cutin(sprintf("%s%d", .Cutin$, .@b), 4);
        sleep2(.@Speed);
        .@b++;
    }

    cutin(sprintf("%s%d", .Cutin$, .@b), 4);

    if (.Prize_ID[.@Sector] == -1) {
        // Free spin
        if (.Sound_Effects)
            soundeffect("wheel_jackpot.wav", 0);
        announce(sprintf("[%s] : Wow, %dx mais Giros grátis !!!", .EventName$, .Prize_Qty[.@Sector]), bc_blue | bc_self);
        #freewheelfortunespin = #freewheelfortunespin == 0 ? .Prize_Qty[.@Sector] : #freewheelfortunespin + .Prize_Qty[.@Sector];
    } else if (.Prize_ID[.@Sector] == 0) {
        // Nothing
        if (.Sound_Effects)
            soundeffect("wheel_lost.wav", 0);
        announce(sprintf("[%s] : Awwww, sem sorte na aposta, mais sorte no amor ...", .EventName$), bc_blue | bc_self);
    } else {
        // Item
        if (.Sound_Effects)
            soundeffect("wheel_won.wav", 0);
        announce(sprintf("[%s] : %dx %s - Aproveite o seu prêmio!", .EventName$, .Prize_Qty[.@Sector], getitemname(.Prize_ID[.@Sector])), bc_blue | bc_self);
        getitem(.Prize_ID[.@Sector], .Prize_Qty[.@Sector]);
    }

    sleep2(1000);
    if (!countitem(35039) && #freewheelfortunespin < 0) {
        mes("[^0055FFGiro da sorte^000000]", .EventName$);
        mes("Você está sem Moedas da Sorte");
        mes("e não tem mais");
        mes("Giros livres. Volte");
        mes("próxima vez para mais!");
        close();
    }
    return;
    
S_End:
    close2();
OnEnd:
    cutin("", 255);
    end;
    
OnInit:
    .EventName$ = "Roda da Sorte";
    bindatcmd("wheel_of_fortune", "Wheel_of_Fortune::OnCommand", 98,false);
    .Spin_Speed = 50; // Qual é a velocidade básica de rotação? (ms)
    .nbTurns = 2; // Quantas vezes a seta faz uma curva completa, antes de entrar na rotina de parada
    .Sound_Effects = true; // Ativar efeitos sonoros? (true/false)

    // Você deve ter um total de 10 prêmios. NÃO remova 0 ou -1 da matriz e não remova
    // mudar sua ordem.
    setarray(.Prize_ID[1], -1, 12103, 14345, 35039,   617,   607, 35040,   604, 0,  12214);
    setarray(.Prize_Qty[1], 2,     1,     1,     1,     3,     3,     2,     3, 0,     3);

    // Don't touch below
    .Cutin$ = "WheelOfFortune_";
    setarray(.Sector_Range, 1, 10); // Sector range
    setarray(.Cutin_Range, 0, 19); // Cutin range
    end;
}